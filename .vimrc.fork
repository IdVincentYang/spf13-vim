" Modeline and Notes {
" vim: set sw=4 ts=4 sts=4 et tw=78 foldmarker={,} foldlevel=0 foldmethod=marker spell:
"
"                    __ _ _____              _
"         ___ _ __  / _/ |___ /      __   __(_)_ __ ___
"        / __| '_ \| |_| | |_ \ _____\ \ / /| | '_ ` _ \
"        \__ \ |_) |  _| |___) |_____|\ V / | | | | | | |
"        |___/ .__/|_| |_|____/        \_/  |_|_| |_| |_|
"            |_|
"
"   This is the personal .vimrc.fork file of Vincent Yang.
"   While much of it is beneficial for personal use, for overriding
"   some settings setted in .vimrc, I would
"   recommend picking out the parts you want and understand.
"
"   Copyright 2015 Wensheng Yang
"
"   Licensed under the Apache License, Version 2.0 (the "License");
"   you may not use this file except in compliance with the License.
"   You may obtain a copy of the License at
"
"       http://www.apache.org/licenses/LICENSE-2.0
"
"   Unless required by applicable law or agreed to in writing, software
"   distributed under the License is distributed on an "AS IS" BASIS,
"   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
"   See the License for the specific language governing permissions and
"   limitations under the License.
" }

" General {
    " Windows has traditionally used cp1252, so it's probably wise to
    " fallback into cp1252(Windows Latin-1) instead of eg. iso-8859-15.
    " Newer Windows files might contain utf-8 or utf-16 LE so we might
    " want to try them first.
    set fileencodings=ucs-bom,utf-8,chinese,utf-16,cp1252,iso-8859-15

    set background=light

    set nospell                           " Spell checking off

    set gdefault                        " let :s command default with 'g'

    " Setting up the directories {
        set nobackup                  " Backups aren't need ...
        set autoread
        set autowrite
        set formatoptions+=mMj          " let vim can break chinese and join
    " }

" }

" Vim UI {
    set cursorcolumn                 " Highlight current column
    set nofoldenable                  " close auto fold code
    "浏览档案的目录，GUI 版本始有。预设是上一次浏览的目录。就是 GUI版本功能表上的 [File] -> [Open] 会打开的目录。
    set bsdir=buffer

    if has('statusline')
        set fillchars+=stl:\|
    endif
" }

" Formatting {
    set wrap                          " Wrap long lines
    set linebreak
" }

" Key (re)Mappings {
    " fast reloading of .vimrc
    nnoremap <silent> <leader>S :source $MYVIMRC<CR>
    nnoremap <leader>ee :e $HOME/.vimrc.fork<CR>
    nnoremap <leader>eb :e $HOME/.vimrc.bundles.fork<CR>
    nnoremap <leader>eB :e $HOME/.vimrc.before.fork<CR>

    " scroll page useing space
    nnoremap <Space> <C-F>
    nnoremap <S-Space> <C-B>

    " use ALT+[jk] to moving lines up and down
    nnoremap <M-j> mz:m+<CR>`z
    nnoremap <M-k> mz:m-2<CR>`z

    nnoremap <C-s-tab> :bp<CR>
    nnoremap <C-tab> :bn<CR>
    nnoremap <leader>Q :bd<CR>
    nnoremap <leader>qq :bp\|bd#<CR>
    nnoremap <leader>qa :bufdo bd<CR>

    command! BcloseOthers call <SID>BufCloseOthers()
    function! <SID>BufCloseOthers()
       let l:currentBufNum   = bufnr("%")
       let l:alternateBufNum = bufnr("#")
       for i in range(1,bufnr("$"))
         if buflisted(i)
           if i!=l:currentBufNum
             execute("bdelete ".i)
           endif
         endif
       endfor
    endfunction
    nnoremap <leader>qo :BcloseOthers<CR>

    "使用CTRL+[hjkl]在窗口间导航
    noremap <C-c> <C-W>c
    noremap <C-j> <C-W>j
    noremap <C-k> <C-W>k
    noremap <C-h> <C-W>h
    noremap <C-l> <C-W>l

    function! OpenInBrowser(file)
        if !exists("g:my_chrome_path")
            if OSX()
                let g:my_chrome_path = '/Applications/Google Chrome.app/'
            elseif WINDOWS()
                let g:my_chrome_path = 'C:\Program Files\Google\Chrome\Application\chrome.exe'
            endif
        endif

        if !exists('g:my_chrome_path')
            echom 'Error: Please set g:my_chrome_path variable in your $MYVIMRC.'
            return
        endif

        if !filereadable(g:my_chrome_path)
            echom 'Found file not exist:' . g:my_chrome_path
            echom 'Error: Please set g:my_chrome_path to locate chrome.'
            return
        endif

        if !filereadable(a:file)
            echom 'Can not open none exists file:' . a:file
            return
        endif

        if WINDOWS()
            let l:run = '!start'
        else
            let l:run = '!open -a '
        endif

        execute 'silent '.l:run.' "'.g:my_chrome_path.'" "'.a:file.'"'
    endfunction
    nnoremap <leader>P :call OpenInBrowser(expand("%:p"))<CR>
" }

" Plugins {
    if isdirectory(expand('~/.vim/bundle/vim-airline'))
        if (g:airline_powerline_fonts)
            if (OSX())
                set guifont=Literation\ Mono\ Powerline:h12
            endif
        endif

        " Add 光标处字符 Unicode 编码|tab长度|字符集|文件类型
        " statusline syntax note: http://gundamew.logdown.com/posts/246352-vim-statusline-syntax-note
        let g:airline_section_y = '0x%B|TS=%{&tabstop}|%{&fenc}[%{&ff}]'
    endif

    if isdirectory(expand('~/.vim/bundle/mru'))
        let MRU_Window_Height = 15 "default is 8"
    endif

    if isdirectory(expand('~/.vim/bundle/gundo.vim'))
        nnoremap <leader>u :GundoToggle<CR>
    endif

    " SnipMate {
        " Setting the author var
        let g:snips_author = 'Vincent Yang <idvincentyang@gmail.com>'
    " }

    if isdirectory(expand('~/.vim/bundle/tabular'))
        function! s:align()
          let p = '^\s*|\s.*\s|\s*$'
          if exists(':Tabularize') && getline('.') =~# '^\s*|' && (getline(line('.')-1) =~# p || getline(line('.')+1) =~# p)
            let column = strlen(substitute(getline('.')[0:col('.')],'[^|]','','g'))
            let position = strlen(matchstr(getline('.')[0:col('.')],'.*|\s*\zs.*'))
            Tabularize/|/l1
            normal! 0
            call search(repeat('[^|]*|',column).'\s\{-\}'.repeat('.',position),'ce',line('.'))
          endif
        endfunction

        inoremap <silent> <Bar>   <Bar><Esc>:call <SID>align()<CR>a
    endif

    if isdirectory(expand('~/.vim/bundle/unite.vim'))
        let g:unite_source_history_yank_enable = 1
        " call unite#filters#matcher_default#use(['matcher_fuzzy'])

        nnoremap <Leader>F  :<C-u>Unite -no-split -buffer-name=unite_grep grep<CR>
        nnoremap <Leader>fb :<C-u>Unite -no-split -buffer-name=unite_buffer -start-insert buffer<CR>
        nnoremap <Leader>fd :<C-u>UniteWithProjectDir -no-split -buffer-name=unite_directory -start-insert directory<CR>
        nnoremap <Leader>ff :<C-u>UniteWithProjectDir -no-split -buffer-name=unite_file_rec -start-insert file_rec/async<CR>
        nnoremap <Leader>fg :<C-u>UniteWithProjectDir -no-split -buffer-name=unite_file_git -start-insert file_rec/git<CR>
        nnoremap <Leader>fl :<C-u>Unite -no-split -buffer-name=unite_line -start-insert line<CR>
        nnoremap <Leader>fo :<C-u>Unite -no-split -buffer-name=unite_outline -start-insert outline<CR>
        nnoremap <leader>fr :<C-u>Unite -no-split -buffer-name=unite_register register<CR>
        nnoremap <Leader>fs :<C-u>Unite -no-split -buffer-name=unite_source -start-insert source<CR>
        nnoremap <Leader>ft :<C-u>Unite -buffer-name=unite_tab tab<CR>
        nnoremap <Leader>fw :<C-u>Unite -buffer-name=unite_window window<CR>

        " Custom mappings for the unite buffer
        function! s:unite_settings()
            " Play nice with supertab
            "let b:SuperTabDisabled=1
            nmap <buffer> <C-J> <Plug>(unite_loop_cursor_down)
            nmap <buffer> <C-K> <Plug>(unite_loop_cursor_up)
            imap <buffer> <C-J> <Plug>(unite_insert_leave)
            imap <buffer> <C-K> <Plug>(unite_insert_leave)kk
            nmap <buffer> <F5> <Plug>(unite_redraw)
            imap <buffer> <F5> <Plug>(unite_redraw)

            "inoremap <buffer> <LocalLeader><BS> <Plug>(unite_delete_backward_path)
            "inoremap <buffer> ; <Plug>(unite_delete_backward_word)
            "inoremap <buffer><expr> <LocalLeader>/ unite#do_action('narrow')

            let unite = unite#get_current_unite()
            if index(unite.source_names, 'file') > -1 || index(unite.source_names, 'file_mru') > -1
              "nnoremap <silent><buffer><expr> <LocalLeader><CR> unite#do_action('open')
              "inoremap <silent><buffer><expr> <LocalLeader><CR> unite#do_action('open')
            endif
            if index(unite.source_names, 'file_rec') > -1
              "inoremap <buffer> <LocalLeader>. <Plug>(unite_redraw)
            else
              "inoremap <buffer> <LocalLeader>.  **/
            endif
        endfunction
        autocmd FileType unite call s:unite_settings()

        if executable('ack')
          " For file and file_rec
        "  let g:unite_source_rec_async_command = 'ack -f --nofilter'
        "  " Use ag in unite grep source.
          let g:unite_source_grep_command = 'ack'
          let g:unite_source_grep_default_opts = '-i --no-heading --no-color -k -H'
          let g:unite_source_grep_recursive_opt = ''
        endif
    endif

    if isdirectory(expand('~/.vim/bundle/YouCompleteMe'))
        nnoremap <leader>jj :YcmCompleter GoTo<CR>
    endif

    if isdirectory(expand('~/.vim/bundle/ex-showmarks'))
        let g:showmarks_enable = 1
        let showmarks_include = "abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ"

        " Ignore help, quickfix, non-modifiable buffers
        let showmarks_ignore_type = "hqm"

        " Hilight lower & upper marks
        let showmarks_hlline_lower = 1
        let showmarks_hlline_upper = 0
    endif

    if isdirectory(expand('~/.vim/bundle/vim-cmake-project'))
        " Generate Project
        function! ProjectGen()
            if !exists("g:cmake_build_dir")
                let g:cmake_build_dir = 'build'
            endif

            let g:cmake_build_dir = input(
            \   "enter build directory : " ,
            \   g:cmake_build_dir
            \   )

            let build_dir_file_type = getftype(g:cmake_build_dir)
            if (build_dir_file_type == "")
                call mkdir(g:cmake_build_dir, "p")
            elseif(build_dir_file_type != 'dir')
                echo ' is not a directory!'
                return
            endif
            let old_dir = getcwd()
            try
                execute 'CMakeGen '.g:cmake_build_dir
            finally
                execute 'cd '.old_dir
            endtry
        endfunction
        nnoremap <leader>pg :call ProjectGen()<CR>

        " Build Project
        function! ProjectBuild()
            if !exists("g:cmake_build_dir")
                call ProjectGen()
            elseif(!filereadable(expand(g:cmake_build_dir . '/Makefile')))
                call ProjectGen()
            endif

            if !filereadable(expand(g:cmake_build_dir . '/Makefile'))
                return
            endif

            execute 'cd '.g:cmake_build_dir
            make
            cd-
        endfunction
        nnoremap <leader>pb :call ProjectBuild()<CR>

        " Project Run
        function! ProjectRun()

            call ProjectBuild()
            if !filereadable(expand(g:cmake_build_dir . '/Makefile'))
                return
            endif

            if !exists("g:cmake_app_name")
                let g:cmake_app_name = ''
            endif

            let g:cmake_app_name = input(
            \   "enter app name : " ,
            \   g:cmake_app_name
            \   )

            let cmake_app_path = expand(g:cmake_build_dir.'/'.g:cmake_app_name)
            if !filereadable(cmake_app_path)
                return
            endif
            execute '!'.cmake_app_path.'&'
        endfunction
        nnoremap <leader>pr :call ProjectRun()<CR>

        " Project Debug
    endif
" }

" TODO autocommand when edit .vimrc.* files, auto set ft=vim"
